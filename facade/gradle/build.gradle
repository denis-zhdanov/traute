buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "com.gradle.publish:plugin-publish-plugin:0.9.9"
    }
}

plugins {
    id "com.jfrog.bintray" version '1.7.3'
}
apply plugin: 'groovy'
apply plugin: 'java-gradle-plugin'
apply plugin: 'com.gradle.plugin-publish'

def pluginId = 'tech.harmonysoft.oss.traute'

gradlePlugin {
    plugins {
        traute {
            id = pluginId
            implementationClass = 'tech.harmonysoft.oss.traute.gradle.TrauteGradlePlugin'
        }
    }
}

task createClasspathManifest {
    def outputDir = file("$buildDir/$name")

    inputs.files sourceSets.main.runtimeClasspath
    outputs.dir outputDir

    doLast {
        outputDir.mkdirs()
        file("$outputDir/plugin-classpath.txt").text =
                sourceSets.main.runtimeClasspath.join('\n') + '\n' + sourceSets.main.compileClasspath.join('\n')
    }
}

dependencies {
    compileOnly project(':core:common')
    compile project(':core:javac-plugin')

    testCompile gradleTestKit()
    testCompile localGroovy()
    testCompile project(':core:test-common').sourceSets.test.output
    testCompile project(path: ':core:test-common', configuration: 'testCompile')

    testRuntime files(createClasspathManifest)
}

jar {
    into('/')
    from {
        configurations.compile
                .filter { it.name.contains('traute') }
                .collect { it.isDirectory() ? it : zipTree(it) }
    }
}

archivesBaseName = 'traute-gradle'
def pluginDisplayName = 'Traute Gradle plugin'
def pluginDescription = 'A Gradle plugin which eases Traute Javac plugin appliance in Gradle projects'
def pluginUrl = 'http://traute.oss.harmonysoft.tech/facade/gradle/'

uploadArchives {
    repositories {
        mavenDeployer {
            pom.project {
                name pluginDisplayName
                description pluginDescription
                url pluginUrl
            }
        }
    }
}

setupBintray()

pluginBundle {
    website = pluginUrl
    vcsUrl = 'https://github.com/denis-zhdanov/traute'
    description = pluginDescription
    tags = ['javac']

    plugins {
        greetingsPlugin {
            id = pluginId
            displayName = pluginDisplayName
        }
    }
}