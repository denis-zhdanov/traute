apply plugin: 'maven'

version = '1.0.0'

dependencies {
    testCompile project(':core:common')
    testCompile project(path: ':core:test-common', configuration: 'testCompile')
    testCompile project(':core:test-common').sourceSets.test.output
}

tasks.withType(JavaExec) {
    def testDependencies = project(':core:test-common').configurations.testCompile.allDependencies.findAll {
        it.group && it.name && it.version && !it.name.contains('junit') && !it.group.contains('harmonysoft')
    }.collect {
        toMavenDependency(it.group, it.name, it.version)
    }
    def javacPluginProject = project(':core:javac-plugin')
    testDependencies = testDependencies <<
                       """|<dependency>
                          |  <groupId>${javacPluginProject.group}</groupId>
                          |  <artifactId>${javacPluginProject.name}</artifactId>
                          |  <version>${javacPluginProject.version}</version>
                          |  <scope>system</scope>
                          |  <systemPath>${javacPluginProject.buildDir}/libs/${javacPluginProject.archivesBaseName}-${javacPluginProject.version}.jar</systemPath>
                          |</dependency>""".stripMargin()
    systemProperties([
            'trauteTestDependencies': testDependencies.join('\n')
    ])
}

static def toMavenDependency(group, artifact, version) {
    """<dependency>
       |  <groupId>${group}</groupId> 
       |  <artifactId>${artifact}</artifactId> 
       |  <version>${version}</version> 
       |  <scope>compile</scope> 
       |</dependency>""".stripMargin()
}

junitPlatformTest.dependsOn project(':core:javac-plugin').tasks.jar